buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }
    dependencies {
        classpath "gradle.plugin.flavor.pie:promptsign:1.0.2"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.10"
    id "org.jetbrains.kotlin.kapt" version "1.2.10"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

if (System.getenv("JITPACK") == null) {
    apply plugin: "flavor.pie.promptsign"
}

group "de.randombyte"
version "2.1"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "http://repo.bstats.org/content/repositories/releases" }
    maven { url "https://jitpack.io" }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.2.10"
    kapt "org.spongepowered:spongeapi:7.0.0"
    shadow("com.github.randombyte-developer.kosp:kosp:v1.1") { transitive = false }
    shadow "org.bstats:bstats-sponge:1.2"
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.byteitems.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.byteitems.shaded.kosp"
    relocate "org.bstats", "de.randombyte.byteitems.shaded.bstats"

    classifier = null // Remove "-all" suffix from output file name
}
build.dependsOn shadowJar

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}