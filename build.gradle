buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }
    dependencies {
        classpath "gradle.plugin.flavor.pie:promptsign:1.0.2"
    }
}

plugins {
    id "maven"
    id "org.jetbrains.kotlin.jvm" version "1.2.10"
    id "org.jetbrains.kotlin.kapt" version "1.2.10"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

def isJitpack = System.getenv("JITPACK") != null

if (!isJitpack) {
    apply plugin: "flavor.pie.promptsign"
}

group "de.randombyte"
version "2.4.1"

allprojects {
    repositories {
        jcenter()
        maven { url "https://repo.spongepowered.org/maven/" }
        maven { url "https://repo.codemc.org/repository/maven-public" }
        maven { url "https://jitpack.io" }
    }
}

ext.spongeApiVersion = "7.1.0"

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.2.10"
    kapt "org.spongepowered:spongeapi:$spongeApiVersion"
    shadow("com.github.randombyte-developer.kosp:kosp:v2.2.3") { transitive = false }
    shadow "org.bstats:bstats-sponge:1.4"
    shadow(project("api")) { transitive = false }
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.byteitems.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.byteitems.shaded.kosp"

    classifier = null // Remove "-all" suffix from output file name
}
build.dependsOn shadowJar
install.dependsOn shadowJar
if (!isJitpack) {
    signArchives.dependsOn shadowJar
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}